group 'org.icfp2017'

buildscript {
  ext.kotlin_version = '1.1.3-2'

  repositories {
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

apply plugin: 'kotlin'
apply plugin: 'application'

sourceCompatibility = 1.8

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
  compile 'com.google.code.gson:gson:2.2.4'
  compile fileTree(dir: 'clj/jars', include: '*.jar')

  testCompile group: 'junit', name: 'junit', version: '4.12'
  testCompile "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
  testCompile 'org.amshove.kluent:kluent:1.26'
}

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

mainClassName = 'org.icfp2017.Main'

defaultTasks 'run'

def REGISTRATION_CODE = '2f4d2e0a-dfcd-4a7d-958c-75ac4b6d4d3f'

distributions {
  main {
    baseName = "icfp-$REGISTRATION_CODE"
    contents {
      from('dist/exec') { fileMode = 0755 }
      from('dist/PACKAGES')
      from('dist/README')
      into('src', { from { 'src/main' } })
    }
  }
}

task assembleSubmitArchive(type: Zip) {
  from "${distTar.archivePath}"
  archiveName = "icfp-${REGISTRATION_CODE}.tar.gz"
  dependsOn distTar
}
